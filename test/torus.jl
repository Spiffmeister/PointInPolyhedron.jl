using PointInPolyhedron
using StaticArrays
using Random
using Test

function toroidal_surface_point(θ, ζ, R₀=5.0, a=1.0)
    x = +(R₀ + a * cos(θ)) * cos(ζ)
    y = -(R₀ + a * cos(θ)) * sin(ζ)
    z = a * sin(θ)
    return SVector(x, y, z)
end

function triangulate_toroidal_surface(θₙ, ζₙ)
    Δθ = π / Float64(θₙ)
    Δζ = π / Float64(ζₙ)

    θ = range(0.0 + Δθ, 2π + Δθ, θₙ + 1)[1:end-1]
    ζ = range(0.0 + Δζ, 2π + Δζ, ζₙ + 1)[1:end-1]

    vertices = Tuple(toroidal_surface_point(t, z) for t in θ for z in ζ)

    # Upper and lower triangle shape are
    # (i, j+1) --- (i+1, j+1)
    #   |   \       |
    #   |    \      |
    # (i, j) --- (i+1, j)
    # In linear indexing
    #
    # (i + ζₙ) -- (i + ζₙ + 1)
    #   |   \       |
    # (i)    --   (i + 1)
    #
    #
    # Lower triangle
    # (i, i + ζₙ, i + 1) for i in 1:θₙ
    # Upper triangle
    # (i + 1, i + ζₙ, i + ζₙ + 1)

    ζθ_indices = LinearIndices((ζₙ, θₙ))

    lower_triangle = (SVector(ζθ_indices[i, j], ζθ_indices[i, mod1(j + 1, θₙ)], ζθ_indices[mod1(i + 1, ζₙ), j]) for i in 1:ζₙ for j in 1:θₙ)
    upper_triangle = (SVector(ζθ_indices[mod1(i + 1, ζₙ), j], ζθ_indices[i, mod1(j + 1, θₙ)], ζθ_indices[mod1(i + 1, ζₙ), mod1(j + 1, θₙ)]) for i in 1:ζₙ for j in 1:θₙ)
    connections = Tuple(Iterators.flatten(zip(lower_triangle, upper_triangle)))

    return vertices, connections
end


θₙ = 100
ζₙ = 500

verts, conns = triangulate_toroidal_surface(θₙ, ζₙ);

m = Mesh{Float64}(verts, conns);


"""
    Florians test particles
"""

Random.seed!(123)
testpt() = toroidal_surface_point(rand() * 2π, rand() * 2π, 5.1, rand() * 0.27 + 0.82)
generate_test_points(ntest=400) = Tuple(testpt() for _ in 1:ntest)

# Generate the particles
test_points = generate_test_points();

function inside_torus(X, R₀=5.0, a=1.0)
    # Distance to axis
    dR = sqrt(X[1]^2 + X[2]^2) - R₀
    # Height above axis
    dZ = X[3]
    #If inside sign == -1, outside sign == 1, on surface == 0
    return sign(sqrt(dR^2 + dZ^2) - a)
end

exact_inside_outside = map(inside_torus, test_points);
exact_inside = Tuple(Float64(all(inout .< 0.0)) for inout in exact_inside_outside)


@testset "Solid angle method" begin
    inout_solidangle = solid_angle(m, test_points)
    inout_solidangle = Float64.(inout_solidangle .> 0.5)

    ysa = zeros(length(test_points))
    solid_angle!(ysa, m, test_points)
    ysa = Float64.(ysa .> 0.5)

    @test all(inout_solidangle .== exact_inside)
    @test all(ysa .== exact_inside)
end

@testset "Winding number method" begin
    inout_winding = winding_number(m, test_points)
    ywn = zeros(length(test_points))
    winding_number!(ywn, m, test_points)

    # 0 if inside
    @test all(inout_winding .== exact_inside)
    @test all(ywn .== exact_inside)
end
